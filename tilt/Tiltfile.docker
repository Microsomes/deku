custom_build(
  'ghcr.io/marigold-dev/deku', # image name, should match with what's in docker-compose
  'nix build .#docker && docker load < ./result',
  ["./src", "./ppx_lambda_vm", "./ppx_let_binding", "./nix"], # folders to watch for changes
  skips_local_docker=False,
  tag = "latest")

def get_services(compose):
  return compose.get("services").keys()

def make_deku_yaml(deku_nodes):
  services = []

  for i in range(len(deku_nodes)):
    services.append((deku_nodes[i], {
      'container_name': deku_nodes[i],
      'restart': 'always',
      'image': 'ghcr.io/marigold-dev/deku',
      'expose': [ 4040, 9000 ],
      'ports': [ "444%s:4040" % i ],
      'volumes': [ ("./data/%s:/app/data" % i) ],
      'command': [ "/app/data", "--listen-prometheus=9000", "-vv" ]
    }))


  prometheus_config = {
    'scrape_configs': [
      {
        'job_name': 'deku',
        'scrape_interval': '1s',
        'metrics_path': '/',
        'static_configs': [
          {
            'targets': [ s[0] + ":9000" for s in services]
          }
        ]
      }
    ]
  }

  prometheus_config_yaml = encode_yaml(prometheus_config)
  output_path = "/tmp/prometheus_config.yaml"

  # Print prometheus_config_yaml into tmp
  local('cat > {}'.format(output_path), stdin=prometheus_config_yaml, echo_off=True, quiet=True)

  # override prometheus with one that works in docker
  services.append(("prometheus", {
    'container_name': 'deku_prometheus',
    'restart': 'always',
    'image': 'prom/prometheus',
    'ports': ['9090:9090'],
    'expose': [ 9090 ],
    'volumes': [ output_path + ':/etc/prometheus/prometheus.yml' ],
    'network_mode': 'bridge',
    'links': deku_nodes,
  }))

  services = {k: v for k, v in services}
  return encode_yaml({
    'version': '3.8',
    'services': services
  })

def load_deku_services (deku_nodes):
  for deku_service in deku_nodes:
    dc_resource(deku_service, labels=["deku"], resource_deps=["deku-setup"])

  # bootstrap the deku network and run some tests
  local_resource(
    "smoke-test",
    "nix run .#sandbox smoke-test docker %s" % len(deku_nodes),
    resource_deps=deku_nodes,
    labels=["scripts"],
    auto_init=config.tilt_subcommand == "ci", # trigger once at start if we're running ci
    trigger_mode=TRIGGER_MODE_MANUAL,
    allow_parallel=True,
    )

  local_resource(
    'deku-net',
    "nix run .#sandbox start docker %s" % len(deku_nodes),
    env={"DATA_DIRECTORY": "./data"},
    resource_deps=deku_nodes,
    labels=["scripts"],
    auto_init=config.tilt_subcommand != "ci", # trigger once at start unless we're running ci
    allow_parallel=False,
    trigger_mode=TRIGGER_MODE_MANUAL,
    )

def add_sandbox (no_of_deku_nodes):
  # run setup when we build
  local_resource(
    "deku-setup",
    "nix run .#sandbox setup docker %s" % no_of_deku_nodes,
    resource_deps=["flextesa"],
    labels=["scripts"],
    )
    
